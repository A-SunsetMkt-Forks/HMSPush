plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "one.yufz.hmspush"
        minSdk 26
        targetSdk 33
        versionCode gitVersionCode
        versionName gitVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        archivesBaseName = "${rootProject.name}-v${versionName}-${versionCode}"
    }

    signingConfigs {
        release {
            storeFile file(STORE_FILE_PATH)
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
            storePassword STORE_PASSWORD
        }
    }

    buildTypes {
        debug {
            if (signingConfigs.release.storeFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
        release {
            if (signingConfigs.release.storeFile.exists()) {
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}
def lifecycle_version = "2.2.0"
def activity_version = "1.6.0"
def fragment_version = "1.5.3"
def preference_version = "1.2.0"


dependencies {
    compileOnly 'de.robv.android.xposed:api:82'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4"

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.activity:activity-ktx:$activity_version"
    implementation "androidx.fragment:fragment-ktx:$fragment_version"
    implementation "androidx.preference:preference-ktx:$preference_version"


    implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:4.3'
}
